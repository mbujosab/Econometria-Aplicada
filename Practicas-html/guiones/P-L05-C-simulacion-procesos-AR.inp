# Los dos primeros comandos son necesarios para que Gretl guarde los resultados de la práctica en el directorio de trabajo
# al ejecutar lo siguiente desde un terminal (use los nombres y ruta que correspondan)
#
#    DIRECTORIO="Nombre_Directorio_trabajo" gretlcli -b ruta/nombre_fichero_de_la_practica.inp
#
# Si esto no le funciona en su sistema, comente las siguientes dos líneas y sitúese en el directorio de trabajo de gretl
# que corresponda (configure dicho directorio de trabajo desde la ventana principal de Gretl).

string directory = getenv("DIRECTORIO")
set workdir "@directory"

function series SimuladorAR(matrix phi)
    # SimuladorAR(phi) simula un proceso AR(p), 
    # donde phi es el polinomio AR y p es su grado.
    scalar p = cols(phi)                             
    series U = normal(0,1)                    
    series Y = 0                              
    setinfo Y --description="Serie simulada"
    loop i = (p+1)..$nobs                         
        scalar comb_pasado_Yt = 0                         
        scalar perturbacion = U[i]    
        loop j = 2..p                         
            comb_pasado_Yt += -phi[1,j] * Y[i-j+1]  # expresión abreviada
        endloop    
        Y[i] = comb_pasado_Yt + perturbacion    
    endloop
    return Y
end function

# establecemos la muestra
nulldata 1500
setobs 12 1900:01 --time-series

scalar n = 0
loop for (r=0; r<=1.1; r+=0.2)
    n += 1
    scalar theta1 = r
    nombre = sprintf("PhiPositivo-%d(%1.1f)", n, theta1)
    sname = sprintf("Caso_%d", n)
    series @sname = SimuladorAR( {1, -theta1} )
    corrgm @sname 12 --plot=@nombre.png
endloop

scalar n = 0
loop for (r=-1; r<=0.1; r+=0.2)
    n += 1
    scalar theta1 = r
    nombre = sprintf("PhiNegativo-%d(%1.1f)", n, theta1)
    sname = sprintf("Caso_%d", n)
    series @sname = SimuladorAR( {1, -theta1} )
    corrgm @sname 12 --plot=@nombre.png
endloop
